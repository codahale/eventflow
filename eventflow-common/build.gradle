plugins {
	id 'idea'
	id 'java-library'
	id 'com.diffplug.spotless'
	id 'com.google.protobuf' version '0.8.13'
}

repositories {
	mavenCentral()
}

dependencies {
	api "com.google.protobuf:protobuf-java:$protobufVersion"
	api "com.google.protobuf:protobuf-java-util:$protobufVersion"

	testImplementation 'junit:junit:4.13.1'
}

sourceSets {
	main {
		proto {
			srcDir '../proto'
		}
	}
}

def generatedJava = file("${protobuf.generatedFilesBaseDir}/main/java")
def generatedGrpc = file("${protobuf.generatedFilesBaseDir}/main/grpc")
idea {
	module {
		generatedSourceDirs += generatedJava
		generatedSourceDirs += generatedGrpc
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:$protobufVersion"
	}
}

compileJava {
	sourceCompatibility = 11
	targetCompatibility = 11
}

spotless {
	java {
		googleJavaFormat()
	}
}

build.dependsOn 'spotlessApply'
